(** {1 Streams} 

Streams are represented as non-mutable, since once an element is a part of the
stream this fact cannot be undone. Length/capacity is non-mutable as well, and
determines the lower bound of the number of elements on the stream.

This library is otherwise loosely based an the standard Why3 "Array" module

*)

module Stream

  use import int.Int
  use import map.Map as M
  use import array.Array

  type stream 'a model { mutable cursor: int; size: int; members: map int 'a }
    invariant { 0 <= self.size }
(* causes trouble?
    invariant { 0 <= self.cursor }
*)

  function get (a: stream ~'a) (i: int): 'a = M.get a.members i

  function atleast (n: int): int
  axiom atleast_def: forall n: int. atleast n >= n

  function view (a: stream ~'a): array 'a   = { length = a.size; elts = a.members }
  function array (a: stream ~'a): array 'a  = { length = a.cursor; elts = a.members }
  function stream (a: array ~'a): stream 'a = { cursor = 0; size = atleast a.length; members = a.elts }
  function preset (a: array ~'a): stream 'a = { cursor = 0; size = a.length; members = a.elts }
 
  val view (a: stream ~'a): array 'a   ensures { result = view a }
  val array (a: stream ~'a): array 'a  ensures { result = array a }
  val stream (a: array ~'a): stream 'a ensures { result = stream a }

  (* These two functions are the same; just different flavours *)
  val enstream (a: stream ~'a) (x: ~'a): unit
  writes { a.cursor }
  ensures { a.cursor = old a.cursor+1 <= a.size }
  ensures { M.get a.members (old a.cursor) = x }

  let destream (a: stream ~'a): 'a
  writes { a.cursor }
  ensures { a.cursor = old a.cursor+1 <= a.size }
  ensures { M.get a.members (old a.cursor) = result }
  = let dummy = any 'a in enstream a dummy; dummy

  function shift (map int ~'a) int: map int 'a
  axiom shift_def:
    forall m: map int 'a. forall i j:int. M.get (shift m j) i  = M.get m (i+j)

  function deriv (a: stream ~'a) (n: int): stream 'a =
    { cursor = a.cursor-n; size = a.size-n; members=shift a.members n }

  val block (a: stream ~'a) (buf: int): unit
  requires { a.cursor < a.size+buf }

  val fresh unit: stream 'a
  ensures { result.cursor = 0 }
 
end

module Pipe

  use import array.Array as A
  use import Stream as S
  use import int.Int

  type pipe 'a = { x: stream 'a; y: stream 'a }
    invariant { self.x.members = self.y.members }
    invariant { 0 <= self.x.cursor <= self.x.size }
    invariant { 0 <= self.y.cursor <= self.y.size }

  function msg (s: pipe ~'a) (n: int): 'a = S.get s.x n

  function sent (s: pipe ~'a): int = s.x.cursor
  function rcvd (s: pipe ~'a): int = s.y.cursor
  function provided (s: pipe ~'a): int = s.x.size
  function requested (s: pipe ~'a): int = s.y.size

  function unused (s: pipe ~'a): bool = rcvd s = sent s = 0
  function unrcvd (s: pipe ~'a): bool = rcvd s = 0
  function unsent (s: pipe ~'a): bool = sent s = 0
 
  function generator (s: pipe ~'a): bool = s.provided > s.requested
  function iterator  (s: pipe ~'a): bool = s.requested > s.provided

  val unbuffered unit: pipe 'a 
  ensures { result.x.cursor = result.y.cursor = 0 }
  ensures { unused result }

  let send (a: pipe ~'a) (value: ~'a)
  writes { a.x.cursor }
  ensures { a.x.cursor = old a.x.cursor+1 <= a.x.size }
  ensures { get a.x (old a.x.cursor) = value }
    = enstream a.x value

  let recv (a: pipe ~'a): 'a
  writes { a.y.cursor }
  ensures { a.y.cursor = old a.y.cursor+1 <= a.y.size }
  ensures { get a.y (old a.y.cursor) = result }
    = destream a.y

  val block_send (a: pipe ~'a): unit
  requires { sent a < requested a }
  ensures { sent a < requested a }

  val block_recv (a: pipe ~'a): unit
  requires { rcvd a < provided a }
  ensures { rcvd a < provided a }

  (* a pipe can be either readable, writable, or blocked *)
  function readable (a: pipe ~'a): bool = requested a < provided a
  val readable (a: pipe ~'a): bool
  ensures { result     -> requested a < provided a }
  ensures { not result -> requested a > provided a }

  function writable (a: pipe ~'a): bool = requested a > provided a
  val writable (a: pipe ~'a): bool
  ensures { result     -> requested a > provided a }
  ensures { not result -> requested a < provided a }

  function blocked (a: pipe ~'a): bool = requested a = provided a
  val blocked (a: pipe ~'a): bool
  ensures { result <-> requested a = provided a }

end

module StreamLTL

  use import Stream
  use import HighOrd
  use import int.Int

  function simple (p: pred 'a) (s: stream 'a): bool = p (get s 0)

  function x (p: pred (stream 'a)) (s: stream 'a): bool 
    = s.size > 0 /\ p (deriv s 1)
  function g (p: pred (stream 'a)) (s: stream 'a): bool
   = forall i: int. 0 <= i < s.size -> p (deriv s i)
  function f (p: pred (stream 'a)) (s: stream 'a): bool 
(* = exists i: int [i]. 0 <= i < s.size /\ p (deriv s i) *)
  axiom f_def: forall p: pred (stream 'a).  forall s: stream 'a. f p s <->
     s.size <> 0 /\ (p s \/ f p (deriv s 1))

  use import array.Array
  constant prv: array int = ( make 4 1 ) [1<-2] [2<-3] [3<-4]

  goal test: simple (\x.x=1) (preset prv)
  goal test_x: x (simple (\x.x=2)) (preset prv)
  goal test_g: g (simple (\x.x>=1)) (preset prv)
  goal test_f: f (simple (\x.x=3)) (preset prv)
    

end

module PipeLTL

  use import Pipe
  use import Stream
  use import HighOrd as H

  function (?) (p: pred (stream 'a)) (s: pipe 'a): bool = p s.x

end

module Test  

  use import Pipe
  use import ref.Refint
  use import int.Int

  let squarer (i: pipe int) (o: pipe int): unit
  diverges
  requires { unused i }
  requires { unused o }
  ensures { rcvd i = sent o }
  ensures { forall n: int. 0 <= n < rcvd i -> msg i n*msg i n = msg o n }
  ensures { writable i }
   = let bla = recv i in
     send o (bla*bla);
     while readable i do
       invariant { 0 <= rcvd i <= requested i }
       invariant { 0 <= sent o <= provided o }
       invariant { rcvd i = sent o }
       invariant { forall n: int. 0 <= n < rcvd i-> msg i n*msg i n = msg o n }
       let x = recv i in
       send o (x*x);
     done

(* this is the 'goroutine' version of squarer; the only thing we change is that
   we replace references to 'sent' and 'rcvd' (which are 'ephemeral') into
   'provided/requested' (which are 'lasting', and the same) *)

  val go_squarer (i: pipe int) (o: pipe int): unit
  requires { unused i }
  requires { unused o }
  ensures { requested i = provided o }
  ensures { forall n: int. 0 <= n < requested i -> msg i n*msg i n = msg o n }
  ensures { writable i }

  let summer (i: pipe int) (o: pipe int): unit
  diverges
  requires { unused i }
  requires { unused o }
  requires { writable o } (* what to do ? *)
  ensures { forall n: int. 0 <= n < sent o -> msg o n = msg i (2*n) + msg i (2*n+1) }
  ensures { rcvd i <= 2*sent o <= rcvd i+1 }
  ensures { writable i }  (* i think this is too permissive *)
  = while readable i do
    invariant { 0 <= rcvd i <= requested i }
    invariant { 0 <= sent o <= provided o }
    invariant { forall n: int. 0 <= n < sent o -> msg o n = msg i (2*n) + msg i (2*n+1) }
    invariant { rcvd i = 2*sent o }
      block_recv i;
      let x = recv i in
      block_recv i;
      let y = recv i in
      block_send o;
      send o (x+y);
      assert { let n = sent o-1 in msg o n = msg i (2*n) + msg i (2*n+1) }
    done

  val go_summer (i: pipe int) (o: pipe int): unit
  requires { unused i }
  requires { unused o }
  ensures { forall n: int. 0 <= n < provided o -> msg o n = msg i (2*n) + msg i (2*n+1) }
  ensures { requested i <= 2*provided o <= requested i+1 }
  ensures { writable i }

  let test_sum unit =
    let a = unbuffered(): pipe int in
    let b = unbuffered(): pipe int in
    go_summer a b;
    block_send a;
    send a 2;
    block_send a;
    send a 3;
    block_recv b;
    let z = recv b in
    check { z = 5 };
    block_send a;
    send a 4;
    block_send a;
    send a 5;
    block_recv b;
    let z = recv b in
    check { z = 9 }

  let test_sqr unit =
    let a = unbuffered(): pipe int in
    let b = unbuffered(): pipe int in
    go_squarer a b;
    block_send a;
    send a 7;
    block_recv b;
    let z = recv b in
    check { z = 49 }

  let bar unit =
    let p = unbuffered(): pipe int in
    let y = recv p in
    send p 5;
    check { y = 5 }

end
